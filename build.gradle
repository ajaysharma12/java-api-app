
apply plugin: 'java'

apply plugin: 'protobuf'
apply plugin: 'war'
apply plugin: 'jetty'

// IDE
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'

// The Gradle wrapper.
task wrapper(type: Wrapper) {
  gradleVersion = '2.1'
}

// Compile with -Xlint
if (!hasProperty('nolint')) {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all"
  }
}

// Sources.
sourceSets {
  main {
    java {
      srcDir 'src/main/java'
    }
    resources {
      srcDir 'src/main/resources'
      srcDir 'src/main/webapp'
    }
  }
  generated {
    java.srcDirs += "$buildDir/$protobuf.outputJava"
  }
}

task createDirs << {
  sourceSets*.java.srcDirs*.each { it.mkdirs() }
  sourceSets*.resources.srcDirs*.each { it.mkdirs() }
}

// Repositories for dependencies.
buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'com.andrewkroh.gradle:gradle-protobuf-plugin:0.3.0'
  }
}

repositories {
  mavenCentral()
}

// Application dependencies.
dependencies {
  compile 'com.google.code.gson:gson:2.+'
  compile 'com.google.guava:guava:18.0'
  compile 'com.google.inject.extensions:guice-servlet:3.0'
  compile 'com.google.inject:guice:3.0'
  compile 'com.google.protobuf:protobuf-java:2.+'
  compile 'com.sun.jersey.contribs:jersey-guice:1.+'
  compile 'com.sun.jersey:jersey-core:1.+'
  compile 'com.sun.jersey:jersey-server:1.+'
  compile 'com.sun.jersey:jersey-servlet:1.+'
  compile 'javax.ws.rs:jsr311-api:1.1.1'
  compile 'joda-time:joda-time:2.+'
  compile 'org.apache.logging.log4j:log4j-api:2.+'
  compile 'org.apache.logging.log4j:log4j-core:2.+'
  compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.+'
  compile 'org.slf4j:slf4j-api:1.+'
  compile 'redis.clients:jedis:2.+'
  providedCompile 'javax.servlet:servlet-api:3.+'
  testCompile 'com.google.truth:truth:0.23'
  testCompile 'junit:junit:4.+'
}

// The WAR.
war {
  baseName = "example"  // name of the WAR and context path.
}

// Jetty
jettyRun {
  reload = "automatic"
  scanIntervalSeconds = 1
}

// API documentation
apply plugin: 'maven'
apply plugin: 'swagger'

buildscript {
  repositories {
    mavenLocal()
  }
  dependencies {
    classpath 'com.github.gradle-swagger:gradle-swagger-plugin:1.0.2-SNAPSHOT'
  }
}
dependencies {
  compile 'com.wordnik:swagger-annotations:1.3.8'
}

swagger {
  endPoints = [
    'in.ajsd.example.api'
  ]
  apiVersion = 'v1'
  basePath = 'http://127.0.0.1:8080/example/api'
  mustacheFileRoot = "${projectDir}/src/main/resources/"
  outputTemplate = "${mustacheFileRoot}/swagger-doc.html.mustache"
  swaggerDirectory = "${buildDir}/site/api-docs"
  outputPath = "${buildDir}/site/swagger/swagger-doc.html"
}

// Misc
configurations.all {
  exclude module: 'slf4j-log4j12'
}
