apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'jetty'

// The Gradle wrapper.
task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
}

// Compile with -Xlint
if (!hasProperty('nolint')) {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all"
  }
}

// Sources.
sourceSets {
  main {
    java {
      srcDir 'src/main/java'
    }
    resources {
      srcDir 'src/main/resources'
      srcDir 'src/main/webapp'
    }
  }
}

task createDirs << {
  sourceSets*.java.srcDirs*.each { it.mkdirs() }
  sourceSets*.resources.srcDirs*.each { it.mkdirs() }
}


// Repositories for dependencies.
repositories {
  mavenCentral()
}

// Application dependencies.
dependencies {
  compile 'com.google.code.gson:gson:2.+'
  compile 'com.google.guava:guava:18.0'
  compile 'com.google.inject.extensions:guice-servlet:3.0'
  compile 'com.google.inject:guice:3.0'
  compile 'joda-time:joda-time:2.+'
  compile 'org.apache.logging.log4j:log4j-api:2.+'
  compile 'org.apache.logging.log4j:log4j-core:2.+'
  compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.+'
  compile 'org.slf4j:slf4j-api:1.+'
  compile 'redis.clients:jedis:2.+'
  compile 'javax.ws.rs:jsr311-api:1.1.1'
  compile 'com.sun.jersey:jersey-server:1.+'
  compile 'com.sun.jersey:jersey-core:1.+'
  compile 'com.sun.jersey:jersey-servlet:1.+'
  compile 'com.sun.jersey.contribs:jersey-guice:1.+'
  //runtime 'org.glassfish.jersey.media:jersey-media-json-jackson:2.+'
  providedCompile 'javax.servlet:servlet-api:3.+'
  testCompile 'junit:junit:4.+'
  testCompile 'com.google.truth:truth:0.23'
}

// Non-versioned libs.
dependencies {
  compile fileTree(dir: 'lib', include: '*.jar')
  runtime fileTree(dir: 'lib', include: '*.jar')
}

// The WAR.
war {
  baseName = "example"  // name of the WAR and context path.
}

// Jetty
jettyRun { 
  reload = "automatic"
  scanIntervalSeconds = 1
}
